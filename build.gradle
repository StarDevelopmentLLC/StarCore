import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar 

plugins {
    id 'common'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.1.0'
}

group = 'com.stardevllc'
version = '0.10.2'

publishing {
    repositories {
        maven {
            name="stardev"
            url="https://repo.stardevllc.com/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    
    publications {
        shadow(MavenPublication) {
            from components.shadow
            
            artifact ("build/libs/${project.name}-${project.version}-javadoc.jar") {
                classifier = "javadoc"
            }
            artifact ("build/libs/${project.name}-${project.version}-sources.jar") {
                classifier = "sources"
            }
        }
    }
}

processResources {
    filesMatching("*.yml") {
        expand(project.properties)
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }

    withJavadocJar()
    withSourcesJar()
}

tasks.named('shadowJar', ShadowJar) {
    archiveClassifier = ''
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    relocate('com.google', 'com.stardevllc.google')
    relocate('net.kyori', 'com.stardevllc.adventure')
    relocate('com.mysql', 'com.stardevllc.mysql')
    relocate('org.yaml.snakeyaml', 'com.stardevllc.config.snakeyaml')
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.21.8-R0.1-SNAPSHOT'
    compileOnly "de.tr7zw:item-nbt-api-plugin:${nbtapiVersion}"

    implementation "com.stardevllc:StarLib:${starlibVersion}"
    implementation "com.stardevllc:StarMCLib:${starmclibVersion}"
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "net.kyori:adventure-api:${adventureVersion}"
    implementation "net.kyori:adventure-platform-bukkit:${adventureBukkitVersion}"
    implementation "net.kyori:adventure-text-minimessage:${miniMessageVersion}"

    implementation "com.stardevllc:StarSQL:${starsqlVersion}"
    implementation "com.stardevllc:config:${starconfigVersion}"
    
    //Project ones
    implementation project(':api')
    implementation project(':v1_8')
    implementation project(':v1_8_3')
    implementation project(':v1_8_8')
    implementation project(':v1_9_2')
    implementation project(':v1_9_4')
    implementation project(':v1_10_2')
    implementation project(':v1_11_2')
    implementation project(':v1_12_2')
    implementation project(':v1_13')
    implementation project(':v1_13_2')
    implementation project(':v1_14_4')
    implementation project(':v1_15_2')
    implementation project(':v1_16_1')
    implementation project(':v1_16_3')
    implementation project(':v1_16_5')
    implementation project(':v1_17_1')
    implementation project(':v1_18_1')
    implementation project(':v1_19_3')
    implementation project(':v1_20_1')
    implementation project(':v1_21_1')
    implementation project(':v1_21_3')
}

tasks.compileJava.dependsOn(clean)
tasks.jar.finalizedBy(shadowJar)
tasks.javadoc.dependsOn(shadowJar)
tasks.sourcesJar.dependsOn(shadowJar)

tasks.withType(PublishToMavenLocal).configureEach {
    dependsOn(shadowJar)
    dependsOn(javadocJar)
    dependsOn(sourcesJar)
}

tasks.withType(PublishToMavenRepository).configureEach {
    dependsOn(shadowJar)
    dependsOn(javadocJar)
    dependsOn(sourcesJar)
}

tasks.build.finalizedBy(publishToMavenLocal)
//tasks.build.finalizedBy(publish)